

////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЗначениеКонстанты, "Константа",
		Объект.Ссылка, , , Истина);
	НастроитьЭлементыДляРежимаПароля();
	Если ЗначениеЗаполнено(Объект.Идентификатор) Тогда

		УстановитьПривилегированныйРежим(Истина);
		Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Идентификатор, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
		Если Пароль <> Неопределено Тогда

			Пароль = ?(ЗначениеЗаполнено(Пароль), ЭтотОбъект.УникальныйИдентификатор, "");

		КонецЕсли;

	КонецЕсли;
	МассивТипов = Метаданные.ОпределяемыеТипы.pr_ТипыЭлементовСпискаКонстанты.Тип.Типы();

	СК = Объект.СписокКонстанты;

	Если СК.Количество() <> 0 Тогда
		ТипЭлементаСписка = ТИПЗНЧ(СК[0].ЭлементСписка);
		Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЭлементаСписка);

	Иначе
		ТипЭлементаСписка = ТИП("Строка");
		Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТИП("Строка"));
	КонецЕсли;

	СписокКонстантыНаФорме.ТипЗначения = Новый ОписаниеТипов(Массив);

	Для Каждого Элемент Из МассивТипов Цикл
		ТипВМассиве = ОбщегоНазначенияклиентСервер.ЗначениеВМассиве(Элемент);
		ДобавляемыйТип = Новый описаниеТипов(ТипВМассиве);
		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		Запись = Новый ЗаписьXML;
		Запись.УстановитьСтроку();
		Сериализатор.ЗаписатьXML(Запись, ДобавляемыйТип);
		ТипСтрокой = Запись.Закрыть();
		Если Элемент = ТипЭлементаСписка Тогда
			ВыбранныйТип = ТипСтрокой;
		КонецЕсли;
		Элементы.ВыбранныйТип.СписокВыбора.Добавить(ТипСтрокой, Строка(ДобавляемыйТип));

	КонецЦикла;

	СписокКонстантыНаФорме.Очистить();
	Для Каждого Строка Из Объект.СписокКонстанты Цикл
		СписокКонстантыНаФорме.Добавить(Строка.ЭлементСписка);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйТипПриИзменении(Элемент)
	СписокКонстантыНаФорме.Очистить();

	УстановитьВыбранныйТипСпискуКонстанты(ВыбранныйТип, СписокКонстантыНаФорме);

КонецПроцедуры
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ПарольИзменен Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Идентификатор, Пароль);
		УстановитьПривилегированныйРежим(Ложь);
		Пароль = ?(ЗначениеЗаполнено(Пароль), ЭтотОбъект.УникальныйИдентификатор, "");
		ПарольИзменен = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.СписокКонстанты.Очистить();
	Для Каждого Элемент Из СписокКонстантыНаФорме Цикл
		нс = ТекущийОбъект.СписокКонстанты.Добавить();
		нс.ЭлементСписка = Элемент.Значение;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЗначениеКонстанты, "Константа",
		Объект.Ссылка, , , Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	ПарольИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЭтоПарольПриИзменении(Элемент)
	НастроитьЭлементыДляРежимаПароля();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Значения

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьВыбранныйТипСпискуКонстанты(ВыбранныйТип, СписокКонстантыНаФорме)
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(ВыбранныйТип);
	сер = Новый СериализаторXDTO(ФабрикаXDTO);

	СписокКонстантыНаФорме.ТипЗначения =  Сер.ПрочитатьXML(Чтение);

КонецПроцедуры

Процедура НастроитьЭлементыДляРежимаПароля()

	ВидимостьДоступность = Не Объект.ЭтоПароль;
	ОбщегоНазначенияклиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСтраницы", "Видимость",
		ВидимостьДоступность);
	ОбщегоНазначенияклиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТипЗначения", "Видимость",
		ВидимостьДоступность);
	ОбщегоНазначенияклиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Логин", "Видимость",
		Не ВидимостьДоступность);
	ОбщегоНазначенияклиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Пароль", "Видимость",
		Не ВидимостьДоступность);

КонецПроцедуры

#КонецОбласти